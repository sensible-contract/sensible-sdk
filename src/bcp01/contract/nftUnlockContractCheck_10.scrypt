import "util.scrypt";
import "nftProto.scrypt";
import "rabinUtil.scrypt";

// opreturn + nftCodeHash + nftID
contract NftUnlockContractCheck {

  static const int MAX_OUTPUT = 10;

  function verifyOutput(bytes output, int nftScriptLen, bytes nftID): bool {
    bytes b = output[8:];
    int n = Util.fromLEUnsigned(b[:1]);
    int sum = 0;
    int offset = 0;
    if (n < 0xfd) {
      sum = 1 + n;
      offset = 1;
    } else if (n == 0xfd) {
      sum = 3 + Util.fromLEUnsigned(b[1:3]);
      offset = 3;
    } else if (n == 0xfe) {
      sum = 5 + Util.fromLEUnsigned(b[1:5]);
      offset = 5;
    } else { // n == 0xff
      // not support 8 bytes length output
      //sum = 9 + Util.fromLEUnsigned(b[1:9]);
      //offset = 9;
      require(false);
    }
    require(len(output) == sum + 8);
    // check if other output is the same token output
    bytes script = output[8 + offset:];
    if (nftScriptLen == len(script)) {
      require(NftProto.getNftID(script, nftScriptLen) != nftID);
    }
    return true;
  }
    
  public function unlock(
    SigHashPreimage txPreimage,
    int nftInputIndex,
    bytes nftScript,
    bytes prevouts,
    bytes rabinMsg,
    bytes[RabinUtil.ORACLE_VERIFY_NUM] rabinPaddingArray,
    int[RabinUtil.ORACLE_VERIFY_NUM] rabinSigArray,
    int[RabinUtil.ORACLE_VERIFY_NUM] rabinPubKeyIndexArray,
    int[RabinUtil.ORACLE_VERIFY_NUM] rabinPubKeyVerifyArray,
    bytes rabinPubKeyHashArray,
    int nOutputs,
    int nftOutputIndex,
    bytes nftOutputAddress,
    int nftOutputSatoshis,
    bytes otherOutputArray
  ) {

    require(Tx.checkPreimage(txPreimage));

    require(hash256(prevouts) == Util.hashPrevouts(txPreimage));

    bytes scriptCode = Util.scriptCode(txPreimage);
    int scriptLen = len(scriptCode);
    bytes nftID = scriptCode[scriptLen - NftProto.NFT_ID_LEN: ];
    bytes nftCodeHash = scriptCode[scriptLen - NftProto.NFT_ID_LEN - 20: scriptLen - NftProto.NFT_ID_LEN];

    int nftScriptLen = len(nftScript);
    require(nftCodeHash == NftProto.getContractCodeHash(nftScript, nftScriptLen));
    require(nftID == NftProto.getNftID(nftScript, nftScriptLen));
    bytes nftAddress = NftProto.getNftAddress(nftScript, nftScriptLen);

    // verify rabin signature
    bytes rabinPubKeyHashArrayHash = NftProto.getRabinPubKeyHashArrayHash(nftScript, nftScriptLen);
    RabinUtil.verifyRabinPubKey(rabinPubKeyHashArrayHash,rabinPubKeyHashArray, rabinPubKeyVerifyArray, rabinPubKeyIndexArray);

    RabinUtil.verifyOtherInput(rabinPubKeyVerifyArray, rabinMsg, rabinPaddingArray, rabinSigArray, prevouts, nftInputIndex, nftScript);

    bool isBurn = false;
    if (nftAddress == NftProto.BURN_ADDRESS) {
      isBurn = true;
    } else {
      require(nftOutputIndex >= 0);
      require(nftOutputIndex < nOutputs);
    }

    // check the outputs
    bytes outputs = b'';
    // max support loop num receiver, you can change this num, but will cause the contrac size increase. you can customize your output
    int otherOutputIndex = 0;
    require(nOutputs <= MAX_OUTPUT);
    loop(MAX_OUTPUT): i {
      if (i < nOutputs) {
        if (i == nftOutputIndex && isBurn == false) {
          bytes outputScript = NftProto.getNewNftScript(nftScript, nftScriptLen, nftOutputAddress);
          bytes output = Util.buildOutput(outputScript, nftOutputSatoshis);
          outputs += output;
        } else {
          int outputLen = Util.fromLEUnsigned(otherOutputArray[otherOutputIndex: otherOutputIndex + 4]);
          bytes output = otherOutputArray[otherOutputIndex + 4: otherOutputIndex + 4 + outputLen];

          // verify output
          this.verifyOutput(output, nftScriptLen, nftID);

          outputs += output;
          otherOutputIndex += 4 + outputLen;
        }
      }
    }
    bytes hashOutputs = hash256(outputs);
    require(hashOutputs == Util.hashOutputs(txPreimage));
  }
}